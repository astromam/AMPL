#################################################################
### AMPL script to design APLC/SP hybrid coronagraph with quarter pupil for ATLAST aperture
### This routine is an updated version of the v9 version code that produces the solutions shown in N'Diaye et al. ApJ 2016
### Written by Mamadou N'Diaye on 2016/09/18
#################################################################
### parameters set by the user
#################################################################
#---------------------
### directory definition
param directoryMain symbolic;
param directoryFPM symbolic;
let directoryMain := "/astro/opticslab1/Users/Mamadou/AMPL/PupilDataFiles/2D/General/CircPupil/";
let directoryFPM := "/astro/opticslab1/Users/Mamadou/AMPL/PupilDataFiles/2D/General/CircPupil/";

#---------------------
### pi defintion
param pi := 4*atan(1);

#---------------------
### spectral bandwidth (la0: arbitrary unit, bw: bandwidth in % of lam0, dlam: bandwidth in arbitrary unit, nlam: spectral sampling)
### optimization in monochromatic light is done by setting bw=0 (and not nlam=1)
param lam0 := 1.;
param bw := 0.10;
param dlam := bw*lam0;
param nlam := 3;

#---------------------
# entrance pupil parameter
param obs := 20;
param spiders :=01;
param gaps :=01;

#---------------------
### FPM radius in lam0/D
param rMask := 8.0/2;

#---------------------
# Lyot stop parameter
param lsobs := 40;
param lsspiders :=02;
param lsgaps :=00;

#---------------------
### diamater ratio between aperture and Lyot stop 
param CoeffOverSizePup :=1.0;

#---------------------
### Outer diameter of each region in lam0/D
param rho0 := 3.5;
param rho1 := 20.0;
param rho2 := 57.0;

#---------------------
### contrast definition in the different regions of the coronagraphic image
param cCoreRegion :=8;
param cDarkHoleRegion :=10;
param cSecondaryRegion :=8;

#---------------------
### discretization parameter (nPup: pupil, nFPM: mask, nImg: image, Fmax: max spatial frequency in image plane)
param nPup := 300;				
param nFPM := 50;
param nImg := 60; # 84;# 

param Fmax := 25; # 35;# 

#################################################################
### filename definitions
#################################################################
#---------------------
### files for import
param filenameAperture symbolic;
param filenameFPM symbolic;
param filenameLyotStop symbolic;
let filenameAperture := "CircPupil_N=0" & nPup & "_obs=" & obs & "_center_quarter_spiders3=0" & spiders & "_gaps=0" & gaps & ".dat";
let filenameFPM := "CircPupil_N=00" & nFPM & "_obs=00_center_quarter.dat";
let filenameLyotStop := "CircPupil_N=0" & nPup & "_obs=" & lsobs & "_center_quarter_spiders3=0" & lsspiders & ".dat";

#---------------------
### files for export
param filenameApodizerDat symbolic;
param filenameApodizerLog symbolic;
let filenameApodizerDat := "BPLC_obs=" & obs & "_sp3=0" & spiders  & "_gaps=0" & gaps & "_FPM=" & round(rMask*100) & "_ls=" & lsobs & "_lssp=0" & lsspiders  & "_OD=" & CoeffOverSizePup*100 & "_IWA=0" & rho0*10 & "_OWA=" & rho1*10 & "_BW=" & round(bw*100) & "_C=" & cDarkHoleRegion & "_2D_N=0" & nPup & "_quarter_gurobi.dat";
let filenameApodizerLog := "BPLC_obs=" & obs & "_sp3=0" & spiders  & "_gaps=0" & gaps & "_FPM=" & round(rMask*100) & "_ls=" & lsobs & "_lssp=0" & lsspiders  & "_OD=" & CoeffOverSizePup*100 & "_IWA=0" & rho0*10 & "_OWA=" & rho1*10 & "_BW=" & round(bw*100) & "_C=" & cDarkHoleRegion & "_2D_N=0" & nPup & "_quarter_gurobi_log.txt";

#################################################################
### vector definitions
#################################################################
#---------------------
### array of wavelengths
set Ls := setof {l in 1..nlam} lam0*(1+((l-1)/(nlam-1)-0.5)*dlam);

#---------------------
### discretization in each plane ({dx,dy}: pupil planes, {dmx, dmy}: first focal plane, {dxi, deta}: final image plane)
param dx := 1/(2*nPup);
param dy := dx;

param dmx := 2.*rMask/(2*nFPM);
param dmy := dmx;

param dxi := (Fmax/nImg)*(1/CoeffOverSizePup);
param deta := dxi;

#---------------------
### coordinate vectors for each plane ({Xs,Ys}: pupil planes, {MXs, MYs}: first focal plane, {Xis, Etas}: final image plane)
set Xs := setof {i in 0.5..nPup-0.5 by 1} i*dx;
set Ys := setof {j in 0.5..nPup-0.5 by 1} j*dy;

set MXs := setof {i in 0.5..nFPM-0.5 by 1} i*dmx;
set MYs := setof {j in 0.5..nFPM-0.5 by 1} j*dmy;

set Xis := setof {i in 0..nImg by 1} i*dxi; 
set Etas := setof {j in 0..nImg by 1} j*deta;

#---------------------
### set of points in the final image plane, only for 360deg masks.
set CoroCoreRegion := setof {xi in Xis, eta in Etas: sqrt(xi^2+eta^2) >= 0 && sqrt(xi^2+eta^2) < rho0} (xi,eta);
set CoroDarkHoleRegion := setof {xi in Xis, eta in Etas: sqrt(xi^2+eta^2) >= rho0 && sqrt(xi^2+eta^2) <= rho1} (xi,eta);
#set CoroSecondaryRegion := setof {xi in Xis, eta in Etas: sqrt(xi^2+eta^2) > rho1 && sqrt(xi^2+eta^2) <= rho2} (xi,eta);

set CoroOuterRegionAll := setof {xi in Xis, eta in Etas: sqrt(xi^2+eta^2) > rho0 && sqrt(xi^2+eta^2) <= rho1} (xi,eta);

#################################################################
### file import and mask definition
#################################################################
#---------------------
### import Pupil, Mask (FPM), and Lyot stop files 
param PupilFile {x in Xs,y in Ys};
read {x in Xs,y in Ys} PupilFile[x,y] < (sprintf("%s%s", directoryMain, filenameAperture)); 
close (sprintf("%s%s", directoryMain, filenameAperture));

param MaskFile {mx in MXs, my in MYs};
read {mx in MXs, my in MYs} MaskFile[mx,my] < (sprintf("%s%s", directoryFPM, filenameFPM));
close (sprintf("%s%s", directoryFPM, filenameFPM));

param LyotFile {x in Xs,y in Ys};
read {x in Xs,y in Ys} LyotFile[x,y] < (sprintf("%s%s", directoryMain,filenameLyotStop));
close (sprintf("%s%s", directoryMain, filenameLyotStop));

#---------------------
### definition of set of points where Pupil, Mask, and Lyot are non null
set Pupil := setof {x in Xs, y in Ys: PupilFile[x,y] != 0.} (x,y);
set Mask := setof {mx in MXs, my in MYs: MaskFile[mx,my] != 0.} (mx,my);
set Lyot := setof {x in Xs, y in Ys: LyotFile[x,y] != 0.} (x,y);

#---------------------
### transmission of the Pupil. Used for calibration.
param TR := sum {x in Xs, y in Ys} PupilFile[x,y]*dx*dy; 

#################################################################
### propagation through APLC coronagraph
#################################################################
#---------------------
### variable A apodizer with transmission to optimize, values ranging between 0 and 1, default value 0.5
var A {x in Xs, y in Ys} >= 0, <= 1, := 0.5;

#---------------------
### electric field within the FPM in plane B
var EBm_real_X {mx in MXs, y in Ys, lam in Ls} := 0.0;
var EBm_real {mx in MXs, my in MYs, lam in Ls} := 0.0;

subject to st_EBm_real_X {mx in MXs, y in Ys, lam in Ls}: EBm_real_X[mx,y,lam] = 2.*sum {x in Xs: (x,y) in Pupil} A[x,y]*PupilFile[x,y]*cos(2.*pi*x*mx*(lam0/lam))*dx;
subject to st_EBm_real {(mx, my) in Mask, lam in Ls}: EBm_real[mx,my,lam] = 2.*(lam0/lam)*sum {y in Ys} EBm_real_X[mx,y,lam]*cos(2.*pi*y*my*(lam0/lam))*dy;

#---------------------
### electric field in plane C (before Lyot stop), be careful, FPM is assumed to be binary. If not, add MaskFile in the expression
var ECm_real_X {x in Xs, my in MYs, lam in Ls} := 0.0;
var ECm_real {x in Xs, y in Ys, lam in Ls} := 0.0;

subject to st_ECm_real_X {x in Xs, my in MYs, lam in Ls}: ECm_real_X[x,my,lam] = 2.*sum {mx in MXs: (mx,my) in Mask} EBm_real[mx,my,lam]*cos(2.*pi*x*mx*(lam0/lam))*dmx;
subject to st_ECm_real {(x,y) in Lyot, lam in Ls}: ECm_real[x,y,lam] = 2.*(lam0/lam)*sum {my in MYs} ECm_real_X[x,my,lam]*cos(2.*pi*y*my*(lam0/lam))*dmy;

#---------------------
### electric field in plane D (final image)
var ED_real_X {xi in Xis, y in Ys, lam in Ls} := 0.0;
var ED_real {xi in Xis, eta in Etas, lam in Ls} := 0.0;

subject to st_ED_real_X {xi in Xis, y in Ys, lam in Ls}: ED_real_X[xi,y,lam] = 2.*sum {x in Xs: (x,y) in Lyot} (A[x,y]*PupilFile[x,y]-ECm_real[x,y,lam])*LyotFile[x,y]*cos(2.*pi*x*xi*(lam0/lam))*dx;
subject to st_ED_real {(xi, eta) in CoroOuterRegionAll, lam in Ls}: ED_real[xi,eta,lam] = 2.*(lam0/lam)*sum {y in Ys} ED_real_X[xi,y,lam]*cos(2.*pi*y*eta*(lam0/lam))*dy;

#---------------------
### electric field peak of the image at lam0 (no coronagraph mask) 
var ED00_real := 0.0;
subject to st_ED00_real: ED00_real = 4.*sum {x in Xs, y in Ys: (x,y) in Lyot} (A[x,y]*PupilFile[x,y]*LyotFile[x,y])*dx*dy;

#################################################################
### optimization problem
#################################################################
#---------------------
### maximization of the apodizer throughput/transmission
maximize throughput: sum{(x,y) in Pupil} A[x,y]*dx*dy/TR;

#---------------------
### constraints in the coronagraphic image core region
### sqrt(2.) factor in the constraints is for margin purpose at all the wavelengths;
#subject to sidelobe_zero_real_pos_core {(xi,eta) in CoroCoreRegion, lam in Ls}: ED_real[xi,eta,lam] <= 10^(-cCoreRegion/2)*ED00_real/sqrt(2.); 
#subject to sidelobe_zero_real_neg_core {(xi,eta) in CoroCoreRegion, lam in Ls}: ED_real[xi,eta,lam] >= -10^(-cCoreRegion/2)*ED00_real/sqrt(2.);

#---------------------
### constraints in the dark hole region
subject to sidelobe_zero_real_pos_darkhole {(xi,eta) in CoroDarkHoleRegion, lam in Ls}: ED_real[xi,eta,lam] <= 10^(-cDarkHoleRegion/2)*ED00_real/sqrt(2.); 
subject to sidelobe_zero_real_neg_darkhole {(xi,eta) in CoroDarkHoleRegion, lam in Ls}: ED_real[xi,eta,lam] >= -10^(-cDarkHoleRegion/2)*ED00_real/sqrt(2.);

#---------------------
### constraints in the secondary dark hole region
#subject to sidelobe_zero_real_pos_darkhole2 {(xi,eta) in CoroSecondaryRegion, lam in Ls}: ED_real[xi,eta,lam] <= 10^(-cSecondaryRegion/2)*ED00_real/sqrt(2.); 
#subject to sidelobe_zero_real_neg_darkhole2 {(xi,eta) in CoroSecondaryRegion, lam in Ls}: ED_real[xi,eta,lam] >= -10^(-cSecondaryRegion/2)*ED00_real/sqrt(2.);

#################################################################
### solver for optimization problem
#################################################################
#---------------------
### options for AMPL
option times 1;
option gentimes 1;
option show_stats 1;
option TMPDIR '/ssdfast1/tmp';

#---------------------
### options for gurobi solver with linear programming method
option solver gurobi;
option gurobi_options "outlev=1 lpmethod=2 crossover=0";

#---------------------
### solve and display of the result
solve;
display solve_result_num, solve_result;

#################################################################
### export solution
#################################################################
#---------------------
### export solution A (apodizer/shaped pupil for APLC) 
printf {x in Xs, y in Ys}: "%15g %15g %15g \n", x, y, A[x,y] > (sprintf("%s%s", directoryMain, filenameApodizerDat));

#---------------------
### log file
option log_file (sprintf("%s%s", directoryMain, filenameApodizerLog));